buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'nexus'

group = 'org.robovm'
archivesBaseName = 'robovm-gradle-plugin'
version = '1.0.0-alpha-01'

sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

ext {
    roboVMVersion = "1.0.0-alpha-01"
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.robovm:robovm-dist-compiler:${roboVMVersion}"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

modifyPom {
    project {
       name 'RoboVM Gradle Plugin'
       packaging 'jar'
       description 'The RoboVM Gradle Plugin provides a way to build RoboVM apps using Gradle.'
       url 'http://www.robovm.com/'
       inceptionYear '2013'

       scm {
           url 'https://github.com/robovm/robovm-gradle-plugin.git'
           connection 'scm:git:git://github.com/robovm/robovm-gradle-plugin.git'
           developerConnection 'scm:git:git@github.com:robovm/robovm-gradle-plugin.git'
       }

       licenses {
           license {
               name 'The Apache Software License, Version 2.0'
               url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
               distribution 'repo'
           }
       }

       developers {
           developer {
               id 'jtakakura'
               name 'Junji Takakura'
           }
           developer {
               id 'ntherning'
               name 'Niklas Therning'
           }
       }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

// apply from: 'local.gradle'
